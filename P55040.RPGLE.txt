      //----------------------------------------------------------------                            
      //                                                                                            
      // Email Stream File To CIF List                                                              
      //                                                                                            
      //                                                                                            
      // CRTRPGMOD MODULE(JDEOBJP/P55040) SRCFILE(JDEOBJP/SERSRCLE)                                 
      //           DBGVIEW(*SOURCE)                                                                 
      // CRTPGM PGM(JDEOBJP/P55040) MODULE(P55040 PCS001)                                           
      //                                                                                            
      // T23335 04.16.2013 Charlie McLean                                                           
      //                                                                                            
      //                                                                                            
      //----------------------------------------------------------------                            
     h option(*srcstmt:*nodebugio) bnddir('QC2LE')                                                  
                                                                                                    
      *     A2 CIF supplemental data base - Code                                                    
     FF01092    IF   E           K disk                                                             
      *     A3 CIF supplemental data base - Narrative                                               
     FF01093LA  IF   E           K disk                                                             
                                                                                                    
     dmypsds          SDS                                                                           
     d $$jobn                244    253                                                             
     d $$user                254    263                                                             
                                                                                                    
     d                 DS                                                                           
     d TimDat                  1     14  0                                                          
     d stime                   1      6  0                                                          
     d smm                     7      8                                                             
     d sdd                     9     10                                                             
     d syy                    11     14                                                             
     d $yy                    13     14                                                             
                                                                                                    
     d                 DS                                                                           
     d tdat                    1      6                                                             
     d tyy                     1      2                                                             
     d tmm                     3      4                                                             
     d tdd                     5      6                                                             
                                                                                                    
      // prototype - send email command                                                             
     d EMLSTMF         PR                  ExtPgm('QCMDEXC')                                        
     d  CmdString                  3000    Options(*Varsize)                                        
     d                                     Const                                                    
     d  CmdLength                    15P 5 Const                                                    
     d                                3    Options(*Nopass)                                         
     d                                     Const                                                    
                                                                                                    
      // prototype - external program call                                                          
     d Runcmd          PR                  ExtPgm('QCMDEXC')                                        
     d                             3000    Const Options(*Varsize)                                  
     d                               15  5 Const                                                    
                                                                                                    
      // prototype for parameters pased in/out                                                      
     d Main            PR                  ExtPgm('P55040')                                         
     d  $$STMF                      256                                                             
     d  $$AN8                         8                                                             
     d  $$SRCM                       32                                                             
      // procedure interface for parameters pased                                                   
     d Main            PI                                                                           
     d  $$STMF                      256                                                             
     d  $$AN8                         8                                                             
     d  $$SRCM                       32                                                             
                                                                                                    
     d Cmd             s           3000    Varying                                                  
                                                                                                    
      // prototypes - IFS APIs                                                                      
      /COPY JDECPY2,CP001                                                                           
                                                                                                    
      //* prototypes - DspError subprocedure                                                        
      /COPY JDECPY2,CP002                                                                           
                                                                                                    
      // variables - IFS APIs used                                                                  
     d FileDesc        s             10I 0   Inz                                                    
     d Oflag           s             10I 0   Inz                                                    
     d Mode            s             10I 0   Inz                                                    
                                                                                                    
      // read API() definitions for oflag and mode parameters                                       
      /COPY JDECPY2,CP003                                                                           
                                                                                                    
      // stand-alone fields                                                                         
     d RC              s             10I 0                                                          
     d buffer          s            200A   Inz(*blank)                                              
     d  buffer@        s               *   Inz(%addr(buffer))                                       
     d $$Dstamp        s             10                                                             
                                                                                                    
     d ##sub           s             15  2                                                          
     d ##tot           s             15  2                                                          
     d $$usrnm         s             10                                                             
     d $$uptxt         s             50                                                             
                                                                                                    
     d path            s            256                                                             
     d #tdat           s              6  0                                                          
                                                                                                    
      // constants                                                                                  
     d CodePage        c                   437                                                      
     d CRLF            c                   X'0D25'                                                  
     d $$pid           c                   'P55040'                                                 
     d comma           c                   ','                                                      
     d period          c                   '.'                                                      
                                                                                                    
      // stuff added for email                                                                      
     d Send            s           3000                                                             
     d p4a             s              4                                                             
     d p4b             s             80                                                             
     d p4c             s             80                                                             
     d pos1            s              4  0                                                          
     d pos2            s              4  0                                                          
     d start2          s              4  0                                                          
     d start3          s              4  0                                                          
     d long            s              4  0                                                          
     d a3k1            s              8  0                                                          
     d a3k2            s              2                                                             
     d a3k3            s              3                                                             
     d a2k1            s              2                                                             
     d a2k2            s              3                                                             
     d a2k3            s              8  0                                                          
     d a2k4            s             10                                                             
     d a2k5            s              6  0                                                          
                                                                                                    
     d $$rtn           s              2                                                             
     d $$prtf          s              1                                                             
     d $$rtxt          s             40                                                             
     d $$rtxt10        c                   'Email sent'                                             
     d $$rtxt50        c                   'No email address found'                                 
     d $$rtxt51        c                   'Incomplete email address'                               
     d $$rtxt52        c                   'Problem with sending mail'                              
                                                                                                    
     d P55040A         s             80    dtaara                                                   
     d P55040B         s             80    dtaara                                                   
     d p1              s             80                                                             
     d p2              s             80                                                             
     d p3              s             80                                                             
     d p4              s             80                                                             
     d p5              s             80                                                             
                                                                                                    
      // email .CSV file to CIF list for AB 34011716 (see S999)                                     
      /Free                                                                                         
        exsr s999;                     // mail from & subject                                       
        exsr s101;                     // start command string                                      
        exsr s102;                     // get send to address(s)                                   
        exsr s103;                     // complete address string                                   
        exsr s104;                     // add .CSV as attachment                                    
        if $$rtn = '10';               // successful EMLSTMG string                                 
          exsr s105;                   // send email                                                
        endif;                                                                                      
      /End-Free                                                                                     
     c                   move      *on           *inlr                                              
                                                                                                    
      // subroutines ----------------------------------------------------                           
                                                                                                    
      //------------------------------------------------------------ S101                           
      // start command string                                                                       
      //                                                                                            
     c     s101          begsr                                                                      
      // build first part of the command string                                                     
     c                   eval        Send = 'MMAIL/EMLSTMF +                                        
     c                               subject(''' + %trim(p1) + ''') +                               
     c                               fromname(''' + %trim(p2) + ''') +                              
     c                               fromaddr(''' + %trim(p3) + ''') +                              
     c                               to('                                                           
     c     e101          endsr                                                                      
                                                                                                    
      //------------------------------------------------------------ S102                           
      // get send-to address(s) for this customer                                                   
      //                                                                                            
     c     s102          begsr                                                                      
      // initialize values for error msg                                                            
     c                   eval      $$prtf = ' '                                                     
     c                   eval      $$rtn  = '10'                                                    
     c                   eval      $$rtxt = $$rtxt10                                                
      // get address(s) for this customer                                                           
     c                   move      *blanks       a3k1                                               
     c                   move      *blanks       a3k2                                               
     c                   move      *blanks       a3k3                                               
     c                   move      $$an8         a3k1                                               
     c                   move      'DL'          a3k2                                               
     c                   movel     'ML'          a3k3                                               
     c     a3key         setll     F01093LA                                                         
     c     s102a         tag                                                                        
     c                   reade     F01093LA                             3536                        
     c     *IN35         ifeq      *ON                                                              
     c     *IN36         oreq      *ON                                                              
     c                   goto      s102b                                                            
     c                   endif                                                                      
                                                                                                    
     c                   move      *blanks       p4a                                                
     c                   move      *blanks       p4b                                                
     c                   move      *blanks       p4c                                                
     c                   move      *blanks       p4                                                 
     c                   movel     A3DS80        p4                                                 
                                                                                                    
     c                   monitor                                                                    
      // parse to-address, email/name/*TO                                                           
     c     '/'           scan      p4            pos1                     90                        
     c     *in90         ifeq      *on                                                              
     c                   eval      long=(pos1-1)                                                    
     c     long          subst     p4:1          p4c                                                
     c                   eval      start2=(pos1+1)                                                  
     c     '/'           scan      p4:start2     pos2                     90                        
     c     *in90         ifeq      *on                                                              
     c                   eval      long=(pos2-start2)                                               
     c     long          subst     p4:start2     p4b                                                
     c                   eval      start3=(pos2+1)                                                  
     c                   eval      long=(%len(p4)-pos2)                                             
     c     long          subst     p4:start3     p4a                                                
     c                   endif                                                                      
     c                   endif                                                                      
     c                   on-error                                                                   
     c                   eval      $$prtf = '1'                                                     
     c                   eval      $$rtn  = '51'                                                    
     c                   eval      $$rtxt = $$rtxt51                                                
     c                   goto      e102                                                             
     c                   endmon                                                                     
      // re-assemble with tic marks and add to command string                                       
     c                   eval        Send = %trim(Send) + ' ''' +                                   
     c                               %trim(p4c) + '''/''' +                                         
     c                               %trim(p4b) + '''/' +                                           
     c                               %trim(p4a)                                                     
     c                   goto      s102a                                                            
     c     s102b         tag                                                                        
      // set error condition if no address is found                                                 
     c     p4            ifeq      ' '                                                              
     c                   eval      $$prtf = '1'                                                     
     c                   eval      $$rtn  = '50'                                                    
     c                   eval      $$rtxt = $$rtxt50                                                
     c                   endif                                                                      
      // set error condition for missing parts                                                      
     c     p4a           ifeq      ' '                                                              
     c     p4b           oreq      ' '                                                              
     c     p4c           oreq      ' '                                                              
     c                   eval      $$prtf = '1'                                                     
     c                   eval      $$rtn  = '51'                                                    
     c                   eval      $$rtxt = $$rtxt51                                                
     c                   endif                                                                      
     c     e102          endsr                                                                      
                                                                                                    
      //------------------------------------------------------------ S103                           
      // finish address portion of command string                                                   
      //                                                                                            
     c     s103          begsr                                                                      
     c                   eval        Send = %trim(Send) + ') stmf('                                 
     c     e103          endsr                                                                      
                                                                                                    
      //------------------------------------------------------------ S104                           
      // add PDF invoice attachments                                                                
      //                                                                                            
     c     s104          begsr                                                                      
     c                   eval        Send = %trim(Send) +                                           
     c                               ' ''' + %trim($$stmf) + ''' '                                  
     c     e104          endsr                                                                      
                                                                                                    
      //------------------------------------------------------------ S105                           
      // complete command string and send email                                                     
      //                                                                                            
     c     s105          begsr                                                                      
     c                   monitor                                                                    
     c                   eval        Send = %trim(Send) + ') +                                      
     c                               msgm(' + $$srcm + ')'                                          
                                                                                                    
     c                   CallP       EMLSTMF(Send : %Len(Send))                                     
     c                   on-error                                                                   
     c                   eval      $$prtf = '1'                                                     
     c                   eval      $$rtn  = '52'                                                    
     c                   eval      $$rtxt = $$rtxt52                                                
     c                   goto      e105                                                             
     c                   endmon                                                                     
     c     e105          endsr                                                                      
                                                                                                    
      //-----------------------------------------------------------  S999                           
      // initialize                                                                                 
      //                                                                                            
     c     S999          begsr                                                                      
     c*                  eval      $$usrnm = W3USRNM                                                
     c*                  eval      ##sub = W3TTIM                                                   
     c*                  eval      $$uptxt = W3UPTXT                                                
      //                                                                                            
     c     A3KEY         KLIST                                                                      
     c                   KFLD                    a3k1                                               
     c                   KFLD                    a3k2                                               
     c                   KFLD                    a3k3                                               
      //                                                                                            
     c     A2KEY         KLIST                                                                      
     c                   KFLD                    a2k1                                               
     c                   KFLD                    a2k2                                               
     c                   KFLD                    a2k3                                               
     c                   KFLD                    a2k4                                               
     c                   KFLD                    a2k5                                               
      // get from-name & from-address, subject                                                      
      /free                                                                                         
         in *dtaara;                                                                                
      /end-free                                                                                     
     c                   eval      p2=%trim(P55040A)                                                
     c                   eval      p3=%trim(P55040B)                                                
      // get email subject from CIF description                                                     
     c                   eval      a2k1 = 'DL'                                                      
     c                   eval      a2k2 = 'ML'                                                      
     c                   eval      a2k3 = %int($$an8)                                               
     c     a2key         setll     F01092                                                           
     c                   reade     F01092                               3536                        
     c     *IN35         ifeq      *ON                                                              
     c     *IN36         oreq      *ON                                                              
     c                   eval      p1 = 'General Report Sent'                                       
     c                   else                                                                       
     c                   eval      p1 = A2RMK                                                       
     c                   endif                                                                      
                                                                                                    
      // time stamp yyyy-mm-12                                                                      
     c                   time                    timdat                                             
     c                   eval      $$dstamp = syy + '-' +                                           
     c                                        smm + '-' + sdd                                       
     c                   eval      tyy = $yy                                                        
     c                   eval      tmm = smm                                                        
     c                   eval      tdd = sdd                                                        
     c                   move      tdat          #tdat                                              
     c                   eval      *in98 = *on                                                      
     c     S999E         endsr                                                                      
